# Maintainer: JP Cimalando <jp-dev@inbox.ru>
pkgname=bambootracker-git
pkgver=0.6.3.r1.g1f197e6e
pkgrel=1
epoch=
pkgdesc="YM2608 (OPNA) music tracker"
arch=('x86_64')
url="https://github.com/rerrahkr/BambooTracker"
license=('GPL2')
depends=('qt5-base' 'jack' 'libpulse' 'hicolor-icon-theme')
makedepends=('git' 'qt5-tools' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/rerrahkr/BambooTracker.git"
        'git+https://github.com/thestk/rtaudio'
        'git+https://github.com/thestk/rtmidi'
        'git+https://github.com/rerrahkr/emu2149#branch=opna-ssg')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "${pkgname%-git}"
  git submodule init
  git config submodule.submodules/RtAudio/src.url $srcdir/rtaudio
  git config submodule.submodules/RtMidi/src.url $srcdir/rtmidi
  git config submodule.submodules/emu2149/src.url $srcdir/emu2149
  git -c protocol.file.allow=always submodule update
}

build() {
cmake -B build -S "${pkgname%-git}" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    cmake --build build
}

package() {
 DESTDIR="$pkgdir" cmake --install build
}
